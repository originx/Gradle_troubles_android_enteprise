apply plugin: 'org.sonarqube'
// Top-level build file where you can add configuration options common to all sub-projects/modules
buildscript {
    ext.kotlin_version = '1.2.10'
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        google()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    dependencies {
        classpath 'com.google.gms:google-services:3.1.2'
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
        classpath "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:2.3.0"
    }
    // use our modified version above.
    configurations {
        androidTestImplementation.exclude group: 'com.android.support', module: 'support-v4'
    }

}

ext {
    revision = getGitCommitHash()
    versionRevision = getGitCommitCount()

    logger.info("Revision ${revision}")
    logger.info("Version revision ${versionRevision}")

    appVersionCode = getVersionCode(ext.versionRevision)
    appVersionNameDE = "5.15.0"
    appVersionNameIT = "2.13.0"
    appVersionNameMX = "3.13.0"
    appVersionNameUS = "3.4.0"

    appVersionNameAT = "1.0.0"

    buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
    isJenkins = System.getenv("JENKINS_HOME") != null

    // override this with -PsendExternalPartners=true to inform external-testers about new version
    sendExternalPartners = project.hasProperty("sendExternalPartners") ? sendExternalPartners.toBoolean() : false
    // override this with -PisProduction=true to create publishable release builds
    isProduction = project.hasProperty("isProduction") ? isProduction.toBoolean() : false
    // override this with -PisBeta=true to enable debug screen in production build without environment switcher
    isBeta = project.hasProperty("isBeta") ? isBeta.toBoolean() : false
    // override this with -PisProximityActivated=true to create publishable release builds
    isProximityActivated = project.hasProperty("isProximityActivated") ? isProximityActivated.toBoolean() : true
    //for releases without proximity change this default value to false

    versions = [
            android: [
                    buildTools : '26.0.2',
                    compileSdk : 26,
                    javaVersion: JavaVersion.VERSION_1_8,
                    minSdk     : 16,
                    targetSdk  : 26
            ],
            libs   : [
                    adition                   : '25',
                    adjust                    : '4.11.4',
                    taptargetview             : '1.9.1',
                    androidanimations         : '2.3',
                    androidMapsUtils          : '0.5+',
                    butterknife               : '8.8.1',
                    checkstyle                : '8.4',
                    commonsCollections4       : '4.1',
                    conceal                   : '1.1.3',
                    confetti                  : '1.1.0',
                    constraintLayout          : '1.0.2',
                    crashlytics               : '2.7.1',
                    cupboard                  : '2.2.0',
                    cupboardTools             : '0.3.1',
                    sqlCipher                 : '3.5.7',
                    dagger                    : '2.13',
                    easing                    : '2.1',
                    eventbus                  : '2.4.0',
                    firebase                  : '11.8.0',
                    glide                     : '4.3.1',
                    gson                      : '2.8.2',
                    joda                      : '2.9.9',
                    jodaConvert               : '1.9.2',
                    jsr250                    : '1.0',
                    ktlint                    : '0.12.1',
                    leakcanary                : '1.5.4',
                    libphonenumber            : '8.8.5',
                    materialprogressbar       : '1.4.2',
                    multidex                  : '1.0.2',
                    okhttp3                   : '3.9.0',
                    omniture                  : '4.13.7',
                    pageindicatorview         : '0.2.0',
                    playServices              : '11.8.0',
                    retrofit                  : '2.3.0',
                    rxandroid2                : '2.0.1',
                    rxbinding                 : '2.0.0',
                    rxcupboard2               : '2.0',
                    rxjava2                   : '2.1.6',
                    securePreferences         : '1.0',
                    support                   : '27.0.2',
                    androidArchitecture       : '1.0.0',
                    androidArchitectureRuntime: '1.0.3',
                    timber                    : '4.5.1',
                    umtcore                   : '0.67',
                    umtsdk                    : '0.67',
                    umtnfc                    : '1.4',
                    validationKomensky        : '0.9.2',
                    zxing                     : '3.3.0',

                    // Only for test
                    dexmaker                  : '2.2.0',
                    junit                     : '4.12',
                    mockito                   : '2.3.0',
                    robolectric               : '3.4-rc3',
                    robolectricShadows        : '3.4-rc3',
            ]
    ]

    configurations.all {
        resolutionStrategy {
            force "com.android.support:support-annotations:" + versions.libs.support,
                    "com.android.support:appcompat-v7:" + versions.libs.support,
                    "com.android.support:support-v4:" + versions.libs.support
        }
    }
}
allprojects {
    // prevent evil gradle children fork processes spawns from stealing focus
    tasks.withType(JavaForkOptions) {
        // Forked processes like GradleWorkerMain for tests won't steal focus!
        jvmArgs '-Djava.awt.headless=true'
    }

    repositories {
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        google()
    }

    apply plugin: 'idea'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}

subprojects {
    tasks.withType(Test).all {
        ignoreFailures isJenkins
        systemProperty "run-under-test", "true"
    }
}

private static def getGitCommitCount() {
    try {
        return 'git rev-list --first-parent --count HEAD'.execute().text.trim() as Integer
    }
    catch (ignored) {
        return 0
    }
}

private static def getGitCommitHash() {
    try {
        return 'git rev-parse --short HEAD'.execute().text.trim()
    }
    catch (ignored) {
        return null
    }
}

private static def getVersionCode(rev) {
    //magic number bcs it was mistakenly changed to max code of DE and applied to all countries now we use it everywhere
    return 680040000 + rev
}

private def getVersionName(appVersionMajor, appVersionMinor, appVersionPatch) {
    return "${appVersionMajor}.${appVersionMinor}.${appVersionPatch}";
}
